# DID 기반 디지털 자격증명 시스템 업데이트 계획

## 1. 현재 개발 현황

- DID 기반 디지털 자격증명 시스템 MVP 개발 완료
- 발급 관리, 유저 관리, VC 관리, VP 관리 기능 구현 완료
- 현재는 Dummy 데이터를 이용한 상태이며, 실제 블록체인 연동만 미완료 상태
- SQLite DB에 관련 데이터 저장 중

## 2. 업데이트 목표

### 2-1. 블록체인 구축 (by-pass)
- Besu 또는 Avalanche 중 선택하여 블록체인을 구축
- EVM 계열 블록체인을 활용, Ethereum과 동일한 방식으로 DID 적용 가능

### 2-2. 블록체인과 연동
- DID Document, VC, VP의 원본 JSON 데이터는 SQLite 또는 IPFS 같은 외부 저장소(Off-Chain)에 저장
- 블록체인에는 해당 데이터의 Hash 값만 저장하여 데이터 무결성 및 진위 여부를 검증

### 2-3. DID Document 처리 방식
- DID Document는 JSON 전체를 블록체인에 저장하지 않고 해시(Hash)값만 저장하여 무결성 유지
- VC 및 VP의 서명 검증을 위해 필요한 Public Key는 DID Document에서 제공

## 3. DID Document 구조

### 기본 필드:
- @context
- id
- controller
- verificationMethod
- authentication
- assertionMethod
- service

ECDSA Secp256k1 키 활용, Web3.js로 생성한 키 사용 가능

### 3-1. DID Document 필수 정보
- DID (id 필드)
- 공개 키 (verificationMethod 필드 내 publicKeyHex)
- 인증 키 (authentication)
- 서명 검증 키 (assertionMethod)
- 서비스 엔드포인트 (service)

## 4. Verifiable Credential(VC) 구성 및 검증

- VC는 DID Document와 연결되어 있으며 발급자(issuer)의 서명을 포함한 JSON 형태로 구성
- VC 데이터는 Off-Chain에 저장하고, 해시값만 블록체인에 기록

### VC 검증 과정:
1. Off-Chain에서 VC JSON 데이터를 가져와 해시 생성
2. 블록체인에 저장된 VC 해시값과 비교하여 무결성 검증
3. DID Document의 공개 키로 VC의 서명 직접 검증

## 5. VP(Verifiable Presentation) 처리 방식

- VP는 사용자가 VC를 선택하여 제출하는 방식으로 구성
- VP 역시 VC와 동일하게 해시값을 블록체인에 기록하여 관리
- VP 검증은 VC와 동일한 방식으로 진행하며, 추가적으로 사용자의 서명을 확인하여 사용자 본인이 제출한 것인지 검증

## 6. 스마트 컨트랙트 개발

#### 스마트컨트랙트 기본 템플릿
- 배포된 컨트랙트 주소 : 0xe191b8988e2a18a86a847142915dd4240c67d710 (사용가능)
- 엔드포인트 : http://3.36.91.35:9650/ext/bc/C/rpc
```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

contract DIDRegistry {
    
    // DID Document 구조 정의
    struct DIDDocument {
        string did;
        string documentHash;
        address owner;
    }

    // VC 상태 enum 정의
    enum VCStatus { NonExistent, Active, Revoked }

    // VC 구조 정의
    struct VC {
        string issuerDid;
        string subjectDid;
        string vcHash;
        VCStatus status;
    }

    // DID 관리 매핑
    mapping(string => DIDDocument) private didDocuments;

    // VC 관리 매핑 (issuerDid => vcHash => VC)
    mapping(string => mapping(string => VC)) private verifiableCredentials;

    // 이벤트 정의
    event DIDCreated(string did, string documentHash, address owner);
    event DIDUpdated(string did, string documentHash);
    event VCRegistered(string issuerDid, string subjectDid, string vcHash);
    event VCRevoked(string issuerDid, string vcHash);

    // DID 생성 함수
    function createDID(string memory _did, string memory _documentHash) public {
        require(didDocuments[_did].owner == address(0), "DID already exists");
        didDocuments[_did] = DIDDocument(_did, _documentHash, msg.sender);
        emit DIDCreated(_did, _documentHash, msg.sender);
    }

    // DID 업데이트 함수
    function updateDID(string memory _did, string memory _newDocumentHash) public {
        require(didDocuments[_did].owner == msg.sender, "Not the owner");
        didDocuments[_did].documentHash = _newDocumentHash;
        emit DIDUpdated(_did, _newDocumentHash);
    }

    // DID 조회 함수
    function getDID(string memory _did) public view returns (string memory, address) {
        DIDDocument memory doc = didDocuments[_did];
        require(doc.owner != address(0), "DID does not exist");
        return (doc.documentHash, doc.owner);
    }

    // VC 등록 함수
    function registerVC(string memory _issuerDid, string memory _subjectDid, string memory _vcHash) public {
        DIDDocument memory issuerDoc = didDocuments[_issuerDid];
        require(issuerDoc.owner == msg.sender, "Only issuer can register VC");

        VC storage vc = verifiableCredentials[_issuerDid][_vcHash];
        require(vc.status == VCStatus.NonExistent, "VC already registered");

        verifiableCredentials[_issuerDid][_vcHash] = VC({
            issuerDid: _issuerDid,
            subjectDid: _subjectDid,
            vcHash: _vcHash,
            status: VCStatus.Active
        });

        emit VCRegistered(_issuerDid, _subjectDid, _vcHash);
    }

    // VC 폐기(Revocation) 함수
    function revokeVC(string memory _issuerDid, string memory _vcHash) public {
        DIDDocument memory issuerDoc = didDocuments[_issuerDid];
        require(issuerDoc.owner == msg.sender, "Only issuer can revoke VC");

        VC storage vc = verifiableCredentials[_issuerDid][_vcHash];
        require(vc.status == VCStatus.Active, "VC not active or not exists");

        vc.status = VCStatus.Revoked;

        emit VCRevoked(_issuerDid, _vcHash);
    }

    // VC 상태 조회 함수 (0: NonExistent, 1: Active, 2: Revoked)
    function getVCStatus(string memory _issuerDid, string memory _vcHash) public view returns (uint8) {
        return uint8(verifiableCredentials[_issuerDid][_vcHash].status);
    }
}

```

### DID Registry 스마트 컨트랙트 (DIDRegistry 사용)
- DID 생성, 업데이트, 삭제, 조회 기능
- DID Document 해시값 저장 및 관리

### VC/VP Registry 스마트 컨트랙트 (DIDRegistry 사용)
- VC, VP의 해시 저장 및 조회 기능

## 7. Web3.js를 활용한 블록체인 연동
- DID 생성 및 Document 등록
- VC 및 VP의 해시값을 스마트 컨트랙트에 저장
- VC/VP의 서명을 Web3.js를 통해 검증하여 데이터 무결성 보장

### 7-1. DID Resolver 도입
- DID Resolver를 사용하여 DID Document의 조회와 검증을 블록체인에서 직접 처리 가능
- ethr-did-resolver를 사용하여 DID Document의 공개 키 추출 및 VC/VP 서명 검증


